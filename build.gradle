import org.apache.tools.ant.filters.ReplaceTokens
import java.text.SimpleDateFormat
import org.ajoberstar.grgit.*

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "http://maptool.craigs-stuff.net/repo/"
        }
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.11.2'
        classpath 'com.diffplug.gradle.spotless:spotless:1.3.0-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "maven-publish"
    id "java-library"   // Solution for gradle issue#1118
    id "java"
    id 'findbugs'
    id 'pmd'
    id 'maven'
    id 'com.diffplug.gradle.spotless' version "1.3.0"
    id 'eclipse'
}

ext.gitrepo = Grgit.open(project.file('.'))
ext.compileDate = new Date();
ext.yyyymmdd = (new SimpleDateFormat('yyyyMMDD')).format(ext.compileDate);

String reponame = 'RPTools'
String repourl = 'github.com/'+reponame+'/'+project.name
String userOrganization = 'rptools'

sourceCompatibility = 1.8

if (version == 'unspecified') {
    version = getVersionName()
}

spotless {
    java {
        eclipseFormatFile 'build-resources/eclipse.prefs.formatter.xml'
    }
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'

        // spotless has built-in rules for the most basic formatting tasks
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}

dependencies {
    compile 'antlr:antlr:2.7.6'
    compile 'commons-io:commons-io:2.4'
    compile 'com.thoughtworks.xstream:xstream:1.4.10'
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-logging:commons-logging:1.1.1'
    compile 'commons-net:commons-net:3.2'
    compile 'log4j:log4j:1.2.16'
    compile 'com.withay:withay-util:1.0'
    compile 'de.huxhorn.sulky:de.huxhorn.sulky.3rdparty.jlayer:1.0'
    compile 'net.java.abeille:abeille-formsrt:2.0'
    testCompile 'junit:junit:4.11'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "http://maptool.craigs-stuff.net/repo/"
    }
}

def pomConfig = {
    licenses {
        license {
            name "The General Public License, v2.0"
            url "http://www.gnu.org/licenses/"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "rptools"
            name "rptools"
            email "support@rptools.net"
        }
    }
    scm {
        url "https://"+repourl
    }
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId = project.name
            version    = project.version
            groupId    = 'net.rptools.rplib';
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'RPTools general lib')
                root.appendNode('name', 'RP Lib')
                root.appendNode('url', "https://"+repourl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    String publishProperty = System.getProperty('publish')
    publications = ['bintrayPublication']

    //Whether to run this as dry-run, without deploying
    dryRun = false

    //Whether version should be auto published after an upload
    publish = (publishProperty == "true")
    if (publish) {
        println "Publishing uploaded files:  " + getVersionName()
    } else {
        println "Not publishing uploaded files:  " + getVersionName()
    }

    //Whether to override version artifacts already published
    override = true

    pkg {
        // result is 'rptools/RPTools/rplib'
        userOrg = userOrganization
        repo = reponame
        name = project.name

        // The rest of these are supposed to establish the contents of
        // the various fields at the bintray site, but so far they
        // haven't seemed to propagate.  Will figure out why at some
        // point (but I have hand-edited the 1419 details).
        desc = 'General library of RP-appropriate functionality'
        licenses = ['LGPL-3.0']
        websiteUrl = 'http://'+repourl
        issueTrackerUrl = 'http://'+repourl+'/issues'
        vcsUrl = 'https://'+repourl+'.git'
        //labels = ['blah', 'yada', 'meh']
        publicDownloadNumbers = true
        githubRepo = reponame+'/'+userOrg
        githubReleaseNotesFile = 'README.md'
        version {
            name = System.getProperty('tag')
            if (name == '' || name == null) {
                name = getVersionName()
            }
            // It would be nice if 'desc' could contain "dev" when the
            // third field is an odd number and "stable" when the third
            // field is an even number...
            desc = "Description of " + name
            released = new Date()
            vcsTag = name
            gpg {
                sign = true
                passphrase = 'my_passphrase'
            }
            mavenCentralSync {
                sync = false
                user = 'rptools'
                password = 'password'
                close = '1'
            }
        }
    }
}

/*
* Gets the version name from project properties or 'version.txt'
*/
def getVersionName() {
    if (project.hasProperty('buildVersion')) {
        return buildVersion
    } else {
        String vtxtVersionNo = new File('build-resources/version.txt').text.trim()
        return vtxtVersionNo
    }
}

findbugs {
    ignoreFailures = true
    toolVersion = '3.0.0'
    effort = 'max'
    sourceSets = []  // Empty source set so it wont run during build/check
}

pmd {
    ignoreFailures = true
    sourceSets = []  // Empty source set so it wont run during tebuild/check
}

jar {
    manifest {
        attributes  'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'),
                    'Source-Compatibility': project.sourceCompatibility
    }
    /*
    include("packages/license/COPYING.LGPLv3")
    // Have to include this next line, because all includes are turned
    // off if even one is specified (such as the license file).
    include("**"+"/*.class")    // To work around nested comments!
    */
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*
task dont_deploy() {
    println "Nothing for Gradle to do when deploying libraries."
    println "Travis will move the JARs into 'releases' in GitHub."
}
*/
